//      O R D    L S


::INIT::
        NAMESPACES=['mtk','trd']
        GENERATE_ON='.'
        INCLUDES = [    '#include "components/trading/msg_trd_cli_ls.h"',
                        '#include "components/trading/trd_cli_support.h"',
                        '#include "components/admin/admin.h"',
                        '#include "components/trading/trd_cli_historic.h"']


::INPUTS::
    rq_nw           (rq   :  mtk::trd::msg::RQ_NW_LS)   [{  int nerrors=0;  bool qty_lower_exec=false;(void)qty_lower_exec;  CHECK_REQUEST_AND_WRITE_LAST_REQ      }]
    rq_md           (rq   :  mtk::trd::msg::RQ_MD_LS)   [{  int nerrors=0;  bool qty_lower_exec=false;(void)qty_lower_exec;  CHECK_REQUEST_MOD_AND_WRITE_LAST_REQ  }]
	rq_cc           (rq   :  mtk::trd::msg::RQ_CC_LS)   [{  int nerrors=0;  bool qty_lower_exec=false;(void)qty_lower_exec;  CHECK_REQUEST_AND_WRITE_LAST_REQ      }]

	cf_nw           (cf   :  mtk::trd::msg::CF_NW_LS)   [{  int nerrors=0;  CHECK_CONFIRM_AND_WRITE_LAST_CONF     }]
	cf_md           (cf   :  mtk::trd::msg::CF_MD_LS)   [{  int nerrors=0;  CHECK_CONFIRM_AND_WRITE_LAST_CONF     }]
	cf_cc           (cf   :  mtk::trd::msg::CF_CC_LS)   [{  int nerrors=0;  CHECK_CONFIRM_AND_WRITE_LAST_CONF     }]

	rj_nw           (rj   :  mtk::trd::msg::RJ_NW_LS)   [{  int nerrors=0;  CHECK_REJECT_AND_WRITE_LAST_CONF      }]
	rj_md           (rj   :  mtk::trd::msg::RJ_MD_LS)   [{  int nerrors=0;  CHECK_REJECT_AND_WRITE_LAST_CONF      }]
	rj_cc           (rj   :  mtk::trd::msg::RJ_CC_LS)   [{  int nerrors=0;  CHECK_REJECT_AND_WRITE_LAST_CONF      }]

	cf_ex           (ex   :  mtk::trd::msg::CF_EX_LS)
                        [{
                                int nerrors=0;
                                CHECK_EXEC
                                const mtk::trd::msg::CF_XX_LS& cf(ex);
                                CHECK_CONFIRM_AND_WRITE_LAST_CONF
                        }]

	cf_st           (st   :  mtk::trd::msg::CF_ST_LS)   [{  IF_LAST_CONF_ISNULL__WRITE_IT         }]





::OUTPUTS::
    sig_rq_nw        (rq   :  mtk::trd::msg::RQ_NW_LS)
    sig_rq_md        (rq   :  mtk::trd::msg::RQ_MD_LS)
	sig_rq_cc        (rq   :  mtk::trd::msg::RQ_CC_LS)

	sig_rj_nw        (rj   :  mtk::trd::msg::RJ_NW_LS)
	sig_rj_md        (rj   :  mtk::trd::msg::RJ_MD_LS)
	sig_rj_cc        (rj   :  mtk::trd::msg::RJ_CC_LS)

	sig_cf_nw        (cf   :  mtk::trd::msg::CF_NW_LS)
	sig_cf_md        (cf   :  mtk::trd::msg::CF_MD_LS)
	sig_cf_cc        (cf   :  mtk::trd::msg::CF_CC_LS)


	sig_cf_ex        (ex   :  mtk::trd::msg::CF_EX_LS)

    sig_cf_st        (st   :  mtk::trd::msg::CF_ST_LS)

    sig_changed      (                               )





::COMMON_STATUS_INFO::
      last_confirmation  :  mtk::nullable<mtk::trd::msg::CF_XX_LS>          ({ mtk::nullable<mtk::trd::msg::CF_XX_LS>() })
      last_request       :  mtk::nullable<mtk::trd::msg::RQ_XX_LS>          ({ mtk::nullable<mtk::trd::msg::RQ_XX_LS>() })
      history            :  mtk::CountPtr<mtk::trd::hist::order_historic_dangerous_not_signal_warped      >  ({ mtk::make_cptr(new mtk::trd::hist::order_historic_dangerous_not_signal_warped)       })
      executions         :  mtk::CountPtr<mtk::trd::hist::order_EXECS_historic_dangerous_not_signal_warped>  ({ mtk::make_cptr(new mtk::trd::hist::order_EXECS_historic_dangerous_not_signal_warped) })
      in_market          :  bool                                            ({false})                                           phony
      is_canceled        :  bool                                            ({false})                                           phony
      is_full_executed   :  bool                                            ({false})                                           phony
      has_pending_rq     :  bool                                            ({false})                                           phony
      serrors            :  std::string                                     ({""})



::STATUS::init

::STATUS::w_cfnw
      has_pending_rq          :  bool                                        ({true})

::STATUS::market
      in_market               :  bool                                        ({true})

::STATUS::w_cfmd
      has_pending_rq          :  bool                                        ({true})

::STATUS::w_cfcc
      has_pending_rq          :  bool                                        ({true})

::STATUS::w_cfmdcc
      has_pending_rq          :  bool                                        ({true})

::STATUS::w_cfnwmd
      has_pending_rq          :  bool                                        ({true})

::STATUS::w_cfnwcc
      has_pending_rq          :  bool                                        ({true})

::STATUS::canceled
      is_canceled             :  bool                                        ({true})

::STATUS::executed
      is_full_executed        :  bool                                        ({true})


::STATUS::error
      description        :  std::string                                      ({_})
      [{
            ci->__serrors += std::string(", ") + description();
            mtk::AlarmMsg(mtk::Alarm(MTK_HERE, "trd_cli_ls.fsm", description(), mtk::alPriorCritic, mtk::alTypeNoPermisions));
      }]








::TRANSITIONS::




init    --   rq_nw  &&   invalid_rq          /   , rj_rqnw             ''>   error      ("invalid rqnw ")       //  errors details provided by guardian
init    --   rq_nw                           /   , rq_rqnw             -->   w_cfnw
init    --   rq_md                           /   , rj_rqmd             ''>   error      (method_name)
init    --   rq_cc                           /   , rj_rqcc             ''>   error      (method_name)
init    --   cf_nw                           /   , cf_cfnw             -->   market
init    --   cf_md                           /   , cf_cfmd             -->   market
init    --   cf_cc                           /   , cf_cfcc             ''>   canceled
init    --   rj_nw                                                     ''>   error      (method_name)
init    --   rj_md                                                     ''>   error      (method_name)
init    --   rj_cc                                                     ''>   error      (method_name)
init    --   cf_ex  &&  full_exec            /   , cf_cfex             -->   executed
init    --   cf_ex                           /   , cf_cfex             -->   market
init    --   cf_st  &&  full_exec            /   , cf_cfst             -->   executed
init    --   cf_st  &&  is_rem_qty_zero      /   , cf_cfst             -->   canceled
init    --   cf_st                           /   , cf_cfst             -->   market


w_cfnw  --   rq_nw                           /   , rj_rqnw & rq_cc     ''>   error      (method_name)
w_cfnw  --   rq_md  &&   invalid_rq          /   , rj_rqmd & rq_cc     ''>   w_cfnwcc
w_cfnw  --   rq_md  &&   quantity_lower_ex   /   , rj_rqmd & rq_cc     ''>   w_cfnwcc
w_cfnw  --   rq_md                           /   , rq_rqmd             -->   w_cfnwmd
w_cfnw  --   rq_cc                           /   , rq_cc               -->   w_cfnwcc
w_cfnw  --   cf_nw  &&   invalid_cf          /   , cf_cfnw & rq_cc     ''>   error      ("invalid cfnw ")       //  errors details provided by guardian
w_cfnw  --   cf_nw                           /   , cf_cfnw             -->   market
w_cfnw  --   cf_md                           /   , cf_cfmd & rq_cc     ''>   error      (method_name)
w_cfnw  --   cf_cc                           /   , cf_cfcc             ''>   error      (method_name)
w_cfnw  --   rj_nw                           /   , cf_rjnw             ''>   canceled
w_cfnw  --   rj_md                           /   , cf_rjmd & rq_cc     ''>   error      (method_name)
w_cfnw  --   rj_cc                           /   , cf_rjcc & rq_cc     ''>   error      (method_name)
w_cfnw  --   cf_ex                           /   , cf_cfex & rq_cc     ''>   error      (method_name)
w_cfnw  --   cf_st                                                     ''>   w_cfnw


market  --   rq_nw                           /   , rj_rqnw & rq_cc     ''>   error      (method_name)
market  --   rq_md  &&   invalid_rq          /   , rj_rqmd & rq_cc     ''>   w_cfcc
market  --   rq_md  &&   quantity_lower_ex   /   , rj_rqmd & rq_cc     ''>   w_cfcc
market  --   rq_md                           /   , rq_rqmd             -->   w_cfmd
market  --   rq_cc                           /   , rq_cc               -->   w_cfcc
market  --   cf_nw                           /   , cf_cfnw & rq_cc     ''>   error      (method_name)
market  --   cf_md                           /   , cf_cfmd_nr          ''>   market
market  --   cf_cc  &&   invalid_cf          /   , cf_cfcc_nr & rq_cc  ''>   error	    ("invalid cfcc")   //	perhaps we receive a confirm cancelation on diferent order
market  --   cf_cc                           /   , cf_cfcc_nr          -->   canceled
market  --   rj_nw                           /   , cf_rjnw & rq_cc     ''>   error      (method_name)
market  --   rj_md                           /   , cf_rjmd & rq_cc     ''>   error      (method_name)
market  --   rj_cc                           /   , cf_rjcc & rq_cc     ''>   error      (method_name)
market  --   cf_ex  &&  full_exec            /   , cf_cfex             -->   executed
market  --   cf_ex                           /   , cf_cfex             ''>   market
market  --   cf_st                                                     ''>   market


w_cfmd  --   rq_nw                           /   , rj_rqnw & rq_cc     ''>   error      (method_name)
w_cfmd  --   rq_md  &&   invalid_rq          /   , rj_rqmd & rq_cc     ''>   error      ("invalid rqmd")
w_cfmd  --   rq_md  &&   quantity_lower_ex   /   , rj_rqmd & rq_cc     ''>   w_cfmdcc
w_cfmd  --   rq_md                           /   , rq_rqmd             ''>   w_cfmd
w_cfmd  --   rq_cc                           /   , rq_cc               -->   w_cfmdcc
w_cfmd  --   cf_nw                           /   , cf_cfnw & rq_cc     ''>   error      (method_name)
w_cfmd  --   cf_md  &&   invalid_cf          /   , cf_cfmd & rq_cc     ''>   error      ("invalid cfmd")
w_cfmd  --   cf_md  &&   is_conf_last_rqmd   /   , cf_cfmd             -->   market
w_cfmd  --   cf_md                           /   , cf_cfmd_pend        ''>   w_cfmd
w_cfmd  --   cf_cc  &&   invalid_cf          /   , cf_cfcc_nr & rq_cc  ''>   error	    ("invalid cfcc")  //	perhaps we receive a confirm cancelation on diferent order
w_cfmd  --   cf_cc                           /   , cf_cfcc_nr          -->   canceled
w_cfmd  --   rj_nw                           /   , cf_rjnw & rq_cc     ''>   error      (method_name)
w_cfmd  --   rj_md                           /   , cf_rjmd             ''>   market
w_cfmd  --   rj_cc                           /   , cf_rjcc & rq_cc     ''>   error      (method_name)
w_cfmd  --   cf_ex  &&  full_exec            /   , cf_cfex             -->   executed
w_cfmd  --   cf_ex                           /   , cf_cfex             ''>   w_cfmd
w_cfmd  --   cf_st                                                     ''>   w_cfmd



w_cfcc  --   rq_nw                           /   , rj_rqnw & rq_cc    ''>   error       (method_name)
w_cfcc  --   rq_md                           /   , rj_rqmd            ''>   w_cfcc
w_cfcc  --   rq_cc                           /   , rq_cc              ''>   w_cfcc
w_cfcc  --   cf_nw                           /   , cf_cfnw & rq_cc    ''>   error       (method_name)
w_cfcc  --   cf_md                           /   , cf_cfmd            ''>   w_cfcc
w_cfcc  --   cf_cc  &&   invalid_cf          /   , cf_cfcc_nr & rq_cc ''>   error	    ("invalid cfcc")  //	perhaps we receive a confirm cancelation on diferent order
w_cfcc  --   cf_cc                           /   , cf_cfcc            -->   canceled
w_cfcc  --   rj_nw                           /   , cf_rjnw & rq_cc    ''>   error       (method_name)
w_cfcc  --   rj_md                           /   , cf_rjmd & rq_cc    ''>   error       (method_name)
w_cfcc  --   rj_cc                           /   , cf_rjcc            ''>   market
w_cfcc  --   cf_ex  &&  full_exec            /   , cf_cfex            -->   executed
w_cfcc  --   cf_ex                           /   , cf_cfex            ''>   w_cfcc
w_cfcc  --   cf_st                                                    ''>   w_cfcc



w_cfmdcc  --   rq_nw                           /   , rj_rqnw & rq_cc    ''>   error     (method_name)
w_cfmdcc  --   rq_md  &&   invalid_rq          /   , rj_rqmd            ''>   w_cfmdcc
w_cfmdcc  --   rq_md  &&   quantity_lower_ex   /   , rj_rqmd & rq_cc    ''>   w_cfmdcc
w_cfmdcc  --   rq_md                           /   , rj_rqmd & rq_cc    ''>   w_cfmdcc
w_cfmdcc  --   rq_cc                           /   , rq_cc              ''>   w_cfmdcc
w_cfmdcc  --   cf_nw                           /   , cf_cfnw & rq_cc    ''>   error     (method_name)
w_cfmdcc  --   cf_md  &&   invalid_cf          /   , cf_cfmd & rq_cc    ''>   error     ("invalid cfmd")
w_cfmdcc  --   cf_md  &&   is_conf_last_rqmd   /   , cf_cfmd            -->   w_cfcc
w_cfmdcc  --   cf_md                           /   , cf_cfmd_pend       ''>   w_cfmdcc
w_cfmdcc  --   cf_cc  &&   invalid_cf          /   , cf_cfcc_nr & rq_cc ''>   error	    ("invalid cfcc")  //	perhaps we receive a confirm cancelation on diferent order
w_cfmdcc  --   cf_cc                           /   , cf_cfcc            -->   canceled
w_cfmdcc  --   rj_nw                           /   , cf_rjnw & rq_cc    ''>   error     (method_name)
w_cfmdcc  --   rj_md                           /   , cf_rjmd            ''>   w_cfcc
w_cfmdcc  --   rj_cc                           /   , cf_rjcc            ''>   w_cfmd
w_cfmdcc  --   cf_ex  &&  full_exec            /   , cf_cfex            -->   executed
w_cfmdcc  --   cf_ex                           /   , cf_cfex            ''>   w_cfmdcc
w_cfmdcc  --   cf_st                                                    ''>   w_cfmdcc



w_cfnwmd  --   rq_nw                           /   , rj_rqnw & rq_cc    ''>   error     (method_name)
w_cfnwmd  --   rq_md  &&   invalid_rq          /   , rj_rqmd & rq_cc    ''>   w_cfnwcc
w_cfnwmd  --   rq_md  &&   quantity_lower_ex   /   , rj_rqmd & rq_cc    ''>   w_cfnwcc
w_cfnwmd  --   rq_md                           /   , rq_rqmd            ''>   w_cfnwmd
w_cfnwmd  --   rq_cc                           /   , rq_cc              -->   w_cfnwcc
w_cfnwmd  --   cf_nw  &&   invalid_cf          /   , cf_cfnw & rq_cc    ''>   error     ("invalid cfnw")
w_cfnwmd  --   cf_nw                           /   , cf_cfnw            -->   w_cfmd
w_cfnwmd  --   cf_md                           /   , cf_cfmd & rq_cc    ''>   error     (method_name)
w_cfnwmd  --   cf_cc                           /   , cf_cfcc & rq_cc    ''>   error     (method_name)
w_cfnwmd  --   rj_nw                           /   , cf_rjnw            ''>   canceled
w_cfnwmd  --   rj_md                           /   , cf_rjmd & rq_cc    ''>   error     (method_name)
w_cfnwmd  --   rj_cc                           /   , cf_rjcc & rq_cc    ''>   error     (method_name)
w_cfnwmd  --   cf_ex                           /   , cf_cfex & rq_cc    ''>   error     (method_name)
w_cfnwmd  --   cf_st                                                    ''>   w_cfnwmd


w_cfnwcc  --   rq_nw                           /   , rj_rqnw & rq_cc    ''>   error     (method_name)
w_cfnwcc  --   rq_md                           /   , rq_rqmd & rq_cc    ''>   w_cfnwcc
w_cfnwcc  --   rq_cc                           /   , rq_cc              ''>   w_cfnwcc
w_cfnwcc  --   cf_nw  &&   invalid_cf          /   , cf_cfnw & rq_cc    ''>   error     ("invalid cfnw")
w_cfnwcc  --   cf_nw                           /   , cf_cfnw            -->   w_cfcc
w_cfnwcc  --   cf_md                           /   , cf_cfmd & rq_cc    ''>   error     (method_name)
w_cfnwcc  --   cf_cc                           /   , cf_cfcc & rq_cc    ''>   error     (method_name)
w_cfnwcc  --   rj_nw                           /   , cf_rjnw            ''>   canceled
w_cfnwcc  --   rj_md                           /   , cf_rjmd & rq_cc    ''>   error     (method_name)
w_cfnwcc  --   rj_cc                           /   , cf_rjcc & rq_cc    ''>   error     (method_name)
w_cfnwcc  --   cf_ex                           /   , cf_cfex & rq_cc    ''>   error     (method_name)
w_cfnwcc  --   cf_st                                                    ''>   w_cfnwcc



canceled --   rq_nw                           /   , rj_rqnw & rq_cc     ''>   error     (method_name)
canceled --   rq_md                           /   , rj_rqmd             ''>   canceled
canceled --   rq_cc                           /   , rj_rqcc             ''>   canceled
canceled --   cf_nw                           /   , cf_cfnw & rq_cc     ''>   error     (method_name)
canceled --   cf_md                           /   , cf_cfmd & rq_cc     ''>   error     (method_name)
canceled --   cf_cc                           /   , cf_cfcc & rq_cc     ''>   error     (method_name)
canceled --   rj_nw                           /   , cf_rjnw & rq_cc     ''>   error     (method_name)
canceled --   rj_md                           /   , cf_rjmd             ''>   canceled
canceled --   rj_cc                           /   , cf_rjcc             ''>   canceled
canceled --   cf_ex                           /   , cf_cfex & rq_cc     ''>   error     (method_name)
canceled --   cf_st                                                     ''>   canceled


executed --   rq_nw                           /   , rj_rqnw & rq_cc     ''>   error     (method_name)
executed --   rq_md                           /   , rj_rqmd             ''>   executed
executed --   rq_cc                           /   , rj_rqcc             ''>   executed
executed --   cf_nw                           /   , cf_cfnw & rq_cc     ''>   error     (method_name)
executed --   cf_md                           /   , cf_cfmd & rq_cc     ''>   error     (method_name)
executed --   cf_cc                           /   , cf_cfcc & rq_cc     ''>   error     (method_name)
executed --   rj_nw                           /   , cf_rjnw & rq_cc     ''>   error     (method_name)
executed --   rj_md                           /   , cf_rjmd             ''>   executed
executed --   rj_cc                           /   , cf_rjcc             ''>   executed
executed --   cf_ex                           /   , cf_cfex & rq_cc     ''>   error     (method_name)
executed --   cf_st                                                     ''>   executed



error    --   rq_nw                           /   , rj_rqnw & rq_cc     ''>   error     (method_name)
error    --   rq_md                           /   , rj_rqmd             ''>   error     (method_name)
error    --   rq_cc                           /   , rq_cc               ''>   error     (method_name)
error    --   cf_nw                           /   , cf_cfnw & rq_cc     ''>   error     (method_name)
error    --   cf_md                           /   , cf_cfmd & rq_cc     ''>   error     (method_name)
error    --   cf_cc                           /   , cf_cfcc & rq_cc     ''>   error     (method_name)
error    --   rj_nw                           /   , cf_rjnw & rq_cc     ''>   error     (method_name)
error    --   rj_md                           /   , cf_rjmd & rq_cc     ''>   error     (method_name)
error    --   rj_cc                           /   , cf_rjcc             ''>   error     (method_name)
error    --   cf_ex                           /   , cf_cfex & rq_cc     ''>   error     (method_name)
error    --   cf_st                                                     ''>   error     (method_name)





::MACROS::

        //  progressing  request.................................................
            rq_rqnw     [{  ADD_HIST_RQ(rq, nw)     ci->sig_rq_nw(rq);  ci->sig_changed();  }]

            rq_rqmd     [{  ADD_HIST_RQ(rq, md)     ci->sig_rq_md(rq);  ci->sig_changed();  }]

            /*********  cancelations will be allways generated not progressed
            rq_rq_cc
                [{
                    ci->sig_rq_cc(p);   ci->sig_changed();
                }]
            **************/

        //  progressing  confirmations.................................................
            cf_cfnw         [{  ADD_HIST_CF(cf, nw)     ci->sig_cf_nw(cf);  ci->sig_changed();  }]

            cf_cfmd         [{  ADD_HIST_CF(cf, md)     ci->sig_cf_md(cf);  ci->sig_changed();  }]
            cf_cfmd_nr      [{  ADD_HIST_CF(cf, md)     ci->sig_cf_md(cf);  ci->sig_changed();  }]
            cf_cfmd_pend    [{  ADD_HIST_CF(cf, md)     ci->sig_cf_md(cf);  ci->sig_changed();  }]

            cf_cfcc         [{  ADD_HIST_CF(cf, cc)     ci->sig_cf_cc(cf);  ci->sig_changed();  }]
            cf_cfcc_nr      [{  ADD_HIST_CF(cf, cc)     ci->sig_cf_cc(cf);  ci->sig_changed();  }]

            cf_cfst         [{                          ci->sig_cf_st(st);  ci->sig_changed();  }]

        //  CONFIRM EXECUTIONS..........................................
            cf_cfex         [{  ADD_HIST_CFEX(ex)       ci->sig_cf_ex(ex);  ci->sig_changed();  }]


        //  progressing  rejects.................................................
            cf_rjnw         [{  ADD_HIST_RJ(rj, nw)     ci->sig_rj_nw(rj);  ci->sig_changed();  }]

            cf_rjmd         [{  ADD_HIST_RJ(rj, md)     ci->sig_rj_md(rj);  ci->sig_changed();  }]

            cf_rjcc         [{  ADD_HIST_RJ(rj, cc)     ci->sig_rj_cc(rj);  ci->sig_changed();  }]



        //  rejecting request.................................................
            rj_rqnw [{ CREATE_AND_SEND_REJECT(sig_rj_nw, RJ_NW_LS, MTK_SS(ci->__serrors << " in " << method_name))      ADD_HIST_RJ(csr_rj, nw)      ci->sig_changed();  }]
            rj_rqcc [{ CREATE_AND_SEND_REJECT(sig_rj_cc, RJ_CC_LS, MTK_SS(ci->__serrors << " in " << method_name))      ADD_HIST_RJ(csr_rj, cc)      ci->sig_changed();  }]
            rj_rqmd [{ std::string rj_message = MTK_SS(ci->__serrors << " in " << method_name);
                       if(qty_lower_exec)
                            rj_message =  " quantity lower than executed quantity  " +  rj_message;
                        CREATE_AND_SEND_REJECT(sig_rj_md, RJ_MD_LS, rj_message) ADD_HIST_RJ(csr_rj, md)      ci->sig_changed();  }]


        //  requesting not requested..........................................
            rq_cc
                [{
                    if (last_confirmation().HasValue())
                    {
                        mtk::trd::msg::RQ_XX     rqxx(last_confirmation().Get().invariant, mtk::admin::get_request_info(), mtk::admin::get_control_fluct_info());
                        mtk::trd::msg::RQ_XX_LS  rqxxls(rqxx, last_confirmation().Get().market_pos);
                        mtk::trd::msg::RQ_CC_LS  rqccls(rqxxls);
                        ADD_HIST_RQ(rqccls, cc)
                        ci->sig_rq_cc(rqccls);
                        ci->sig_changed();
                    }
                    else if (last_request().HasValue())
                    {
                        mtk::trd::msg::RQ_XX    rqxx(last_request().Get().invariant, mtk::admin::get_request_info(), mtk::admin::get_control_fluct_info());
                        mtk::trd::msg::RQ_XX_LS rqxxls(rqxx, last_request().Get().request_pos);
                        mtk::trd::msg::RQ_CC_LS rqccls(rqxxls);
                        ADD_HIST_RQ(rqccls, cc)
                        ci->sig_rq_cc(rqccls);
                        ci->sig_changed();
                    }
                    else
                        throw mtk::Alarm(MTK_HERE, "trd_cli_ls.fsm", "Cannot request a cancelation with no previus request or previus confirmation", mtk::alPriorCritic, mtk::alTypeNoPermisions);
                }]







        //  GUARD macros ///////////////////////////////////////////////////////////////////
            invalid_rq
                [{
                    nerrors > 0
                }]

            quantity_lower_ex
                [{
                    qty_lower_exec
                }]

            invalid_cf
                [{
                    nerrors > 0
                }]

            is_conf_last_rqmd
                [{
                    ({  bool result = false;
                        if (ci->last_request().HasValue())
                            result = (cf.req_id == ci->last_request().Get().request_info.req_id);
                        else
                            result = false;
                        result;
                    })
                }]

            full_exec
                [{
                    last_confirmation().HasValue() == true
                    &&  last_confirmation().Get().total_execs.acc_quantity
                        == last_confirmation().Get().market_pos.quantity
                }]

            is_rem_qty_zero
                [{
                    last_confirmation().HasValue() == true
                    &&  last_confirmation().Get().total_execs.remaining_qty.GetIntCode() == 0
                }]
